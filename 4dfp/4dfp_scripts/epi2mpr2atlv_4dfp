#!/bin/csh
# $Header: /data/petsun4/data1/solaris/csh_scripts/RCS/epi2mpr2atlv_4dfp,v 1.12 2007/09/26 02:24:14 avi Exp $
# $Log: epi2mpr2atlv_4dfp,v $
# Revision 1.12  2007/09/26  02:24:14  avi
# usage; eliminate superfluous $bidir; use $RELEASE
#
# Revision 1.11  2007/09/25  17:19:49  mohanar
# Added the -T and -S flags.
#
# Revision 1.10  2005/10/12  02:09:54  avi
# noinit option
#
# Revision 1.9  2005/08/12  23:41:52  avi
# generalize computation of $target
#
# Revision 1.8  2002/06/25  00:34:46  avi
# add atlas targets 711-2[KL]
#
# Revision 1.7  2000/10/24  03:57:44  avi
# accommodate targets 711-2[CYO] and eliminate computing $epi"_to_711-2A_t4"
#
# Revision 1.6  1999/02/22  01:38:08  avi
# eliminate usused definitions ref and refmsk
# Revision 1.5  1999/01/15  23:06:14  avi
# remove refences to $lindir
# Revision 1.4  1999/01/14  03:24:45  avi
# for images in other directories ln -s *.4dfp.{img,img.rec,ifh} instead of cp
# Revision 1.3  1999/01/10  04:39:38  avi
# call Hessian mode imgreg_4dfp twice
# Revision 1.2  1999/01/09  19:23:36  avi
# minimum number of args 3 -> 2
# Revision 1.1  1998/12/31  04:56:21  avi
# Initial revision
set idstr = '$Id: epi2mpr2atlv_4dfp,v 1.12 2007/09/26 02:24:14 avi Exp $'

echo $idstr
if (${#argv} < 2) then
	echo "Usage:	epi2mpr2atlv_4dfp <epi_anat> <mpr_anat> [useold] [atlas target [711-2? OR -T<target including path>] [-S<atlas space>] [noinit]"
	echo "e.g.,	epi2mpr2atlv_4dfp stem9_anat_ave stem9_654-3 useold 711-2C"
	echo "e.g.,	epi2mpr2atlv_4dfp stem9_anat_ave stem9_654-3 useold -T/data/cninds01/atlas/NP765 -S711-2B"
	echo "N.B.:	Any image argument may include a path, e.g., /data/petmr1/data7/stem/96_06_14_stem9/stem9_654-3"
	echo "N.B.:	All named images must be in either ANALYZE or 4dfp format. ANALYZE will be converted to 4dfp"
	echo "N.B.:	The default atlas target is 711-2B"
	echo "N.B.:	'useold' inhibits re-computation of all t4 files"
	echo "N.B.:	'noinit' inhibits initialization of epi->mpr t4 file"
	echo "N.B.:	-S specifies the atlas space. The only currently supported atlas space is 711-2B"
	echo "N.B.:	-S may be used only with -T"
	exit 1
endif

set date0 = `date`
set usr = `whoami`

@ useold = 0
@ noinit = 0
@ debug = 0

set atlas = "711-2B"
set atlasswitch = ""
set hasStudyTarget = 0
set hasAtlas = 0


set wrkdir =	$cwd
if (! $?RELEASE) then
	set bindir = /data/petsun4/data1/solaris
else
	set bindir = $RELEASE
endif
set oristr =	(T C S)

set xxx = ($1 $2)
set xxx = ($xxx:gr)
set xxx = ($xxx:gr)
set tmp = ($xxx:gh)
set epi = $xxx[1]:t
if (-d $tmp[1]:h) then
	set epidir = `pushd $tmp[1]:h | awk '{print $1}'`
else
	set epidir = $cwd
endif
set mpr = $xxx[2]:t
if (-d $tmp[2]:h) then
	set mprdir = `pushd $tmp[2] | awk '{print $1}'`
else
	set mprdir = $cwd
endif

@ k = 1
while ($k <= 2)
	     if (-e $xxx[$k]:t.4dfp.img && -e $xxx[$k]:t.4dfp.ifh) then
	else if (-e $xxx[$k]:t.img && -e $xxx[$k]:t.hdr) then
		analyzeto4dfp $xxx[$k]:t
	else if (-e $xxx[$k]:t.4dint.img && -e $xxx[$k]:t.4dint.hdr) then
		analyzeto4dfp $xxx[$k]:t.4dint
	else if (-e $xxx[$k].4dfp.img && -e $xxx[$k].4dfp.ifh) then
		foreach e (img img.rec ifh)
			ln -s $xxx[$k].4dfp.$e .
		end
	else if (-e $xxx[$k].img && -e $xxx[$k].hdr) then
		/bin/cp $xxx[$k].{img,img.rec,hdr} .
		analyzeto4dfp $xxx[$k]:t
		/bin/rm $xxx[$k]:t.{img,img.rec,hdr}
	else if (-e $xxx[$k].4dint.img && -e $xxx[$k].4dint.hdr) then
		/bin/cp $xxx[$k].4dint.{img,img.rec,hdr} .
		analyzeto4dfp $xxx[$k]:t.4dint
		/bin/rm $xxx[$k]:t.4dint.{img,img.rec,hdr}
	else	echo $xxx[$k] not found
		exit -1
	else
	endif
	@ k++
end

set target = "711-2B"
set targetswitch = "711-2B"
foreach x (${argv[2-]})
	if ($x == "debug")	@ debug++
	if ($x == "useold")	@ useold++		# use existing t4files if present
	if ($x == "noinit")	@ noinit++		# start with existing $epi"_to_"$mpr"_t4"
	switch ($x)
		case 711-2?:
			set target       = $x;
			set targetswitch = $target
			set atlasswitch = ""
			breaksw;
		case -T*:
			set targetpath   = `echo $x | awk '{print substr($1,3)}'`;
			set target       = $targetpath:gt;
			set target       = $target:gr			# trim off extensions
			set target       = $target:gr
			set target       = $target:gr
			set targetswitch = "-T"$targetpath
			set hasStudyTarget = 1;
			breaksw;
		case -S*:
			set atlas   = `echo $x | awk '{print substr($1,3)}'`;
			set hasAtlas = 1;
			set atlasswitch = "-S"$atlas
			breaksw;
	endsw
end

if ($hasStudyTarget == 1) then
	if ($hasAtlas == 0) then
		echo -S option not used. Using default atlas space $atlas
	else
		if ($atlas != "711-2B") then
			echo Unsupported atlas space $atlas used
			exit -1;
		endif
	endif
endif
echo "atlas representative target = "$target "atlas space = "$atlas

set log = $mpr"_epi2mpr2atlv_4dfp".log
touch				$log
date >>				$log
echo $0 $argv[1-] >>		$log
echo $idstr >>			$log
echo epi = $epidir/$epi >>	$log
echo mpr = $mprdir/$mpr >>	$log
echo "target="$target >>	$log
echo "atlas="$atlas >>		$log
tail -50 $log

set t4file = $mpr"_to_"$target"_t4"
if ($useold && ! -e $t4file && -e $mprdir/$t4file) cp $mprdir/$t4file .
if (! $useold || ! -e $t4file) mpr2atl_4dfp $mpr $targetswitch $atlasswitch

set t4file = $epi"_to_"$mpr"_t4"
if ($useold && -e $epidir/$t4file) /bin/cp $epidir/$t4file .
if (! $useold || ! -e $t4file) then
	if (! -e $mpr"_g11".4dfp.img || ! -e $mpr"_g11".4dfp.ifh) gauss_4dfp $mpr 1.1
	if (! -e $epi"_g11".4dfp.img || ! -e $epi"_g11".4dfp.ifh) gauss_4dfp $epi 1.1
	switch ($target)
		case 711-2?:
			echo default call to msktgen_4dfp using standard target  
			msktgen_4dfp $mpr
		breaksw;
		default:
			msktgen_4dfp $mpr $targetswitch $atlasswitch
	endsw
	if ($status < 0) exit $status
	mskgen_4dfp $epi
	if ($status < 0) exit $status

	if (! $noinit) then
		@ ori = `awk '/orientation/{print $NF - 1}' $mpr.4dfp.ifh`
		t4_inv $bindir/$oristr[$ori]_t4 temp$$_t4
		@ ori = `awk '/orientation/{print $NF - 1}' $epi.4dfp.ifh`
		t4_mul $bindir/$oristr[$ori]_t4 temp$$_t4 $t4file
	endif

	@ mode = 1024 + 3
	imgreg_4dfp $mpr"_g11" none        $epi"_g11" $epi"_mskz" $t4file $mode >> $log
	if ($status < 0) exit $status
	@ mode = 1024 + 3
	imgreg_4dfp $mpr"_g11" $mpr"_mskt" $epi"_g11" $epi"_mskz" $t4file $mode >> $log
	@ mode = 2048 + 3 + 8 + 64
	imgreg_4dfp $mpr"_g11" $mpr"_mskt" $epi"_g11" $epi"_mskz" $t4file $mode >> $log
	@ mode = 8192 + 2048 + 3 + 8 + 64
	imgreg_4dfp $mpr"_g11" $mpr"_mskt" $epi"_g11" $epi"_mskz" $t4file $mode >> $log
	imgreg_4dfp $mpr"_g11" $mpr"_mskt" $epi"_g11" $epi"_mskz" $t4file $mode >> $log
	if (! $debug) /bin/rm $mpr"_msk"* $mpr"_g"* $epi"_msk"* $epi"_g"* temp$$_t4
endif

t4_mul $epi"_to_"$mpr"_t4" $mpr"_to_"$target"_t4"	$epi"_to_"$target"_t4"

echo "start time "$date0
echo "end   time "`date`
exit
